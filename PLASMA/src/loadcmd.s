SRC	=	TMP
SRCL	=	SRC
SRCH	=	SRC+1
DST	=	SRC+2
DSTL	=	DST
DSTH	=	DST+1
ESP	=	DST+2
	!PSEUDOPC	$1000 {
;*
;* CLEAR COMMAND LINE LENGTH BYTE IF CALLED FROM 'BYE'
;*
	LDY	#$00
	LDX	#$FE            ; LEAVE ROOM FOR COMMAND LINE LENGTH BYTE
	TXS
        BVS     +
        STY     $01FF            ; CLEAR AUTORUN COMMAND WHEN CALLED FROM 'BYE'
;*
;* MOVE REST OF CMD FROM LANGUAGE CARD
;*
+	STY	$06
	STY	$08
	LDA	#$D2
	STA	$07
	LDA	#$11
	STA	$09
	BIT	LCRDEN+LCBNK2
-	LDA	($06),Y
	STA	($08),Y
	INY
	BNE	-
	INC	$07
	INC	$09
	LDA	$07
	CMP	#$E0
	BNE	-
;*
;* DEACTIVATE 80 COL CARDS
;*
	BIT	ROMEN
	LDY	#4
-	LDA	DISABLE80,Y
	JSR	$FDED
	DEY
	BPL	-
	BIT	$C054		; SET TEXT MODE
	BIT	$C051
	BIT	$C058
	JSR	$FC58		; HOME
;*
;* JUMP TO INTERPRETER
;*
	BIT	LCRDEN+LCBNK2
	LDX	#$00
	LDA	#$BF
	STX	IFPL
	STA	IFPH
	JSR	INTERP
	!BYTE	0
	!WORD	START
DISABLE80 !BYTE	21, 13, '1', 26, 13
	!SOURCE "cmd.a"
}